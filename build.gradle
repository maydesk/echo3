apply plugin: 'java'
apply plugin: 'maven'

group = 'org.maydesk'
sourceCompatibility = JavaVersion.VERSION_1_4
def cloudbeesAccountName = 'maydesk'
def cloudbeesUsername = 'maydesk-deploy'
def cloudbeesPassword = 'secret111'

configurations {
	deployerJars
}

sourceSets {
	main {
		java {
			srcDir 'src/server-java/app'
			srcDir 'src/server-java/webcontainer'
		}
	}
	
	test {
		java {
			srcDir 'src/server-java/app-test'
			srcDir 'src/server-java/webcontainer-test'
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile "javax.servlet:servlet-api:2.+"
	testCompile "junit:junit:4.11"
	deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
	deployerJars 'org.apache.maven:maven-ant-tasks:2.1.0'
}

ant.importBuild 'gradleWrapper.xml'

// Ant build file needs the sevrlet.lib.jar to point to the correct location
ant.properties['servlet.lib.jar'] = configurations.compile.asPath
defaultTasks 'ant.dist'

clean.dependsOn 'ant.clean'
uploadArchives.dependsOn 'ant.dist'

def echoAppFile = file(ant.properties['dir.dist.lib'] + '/' + ant.properties['jarfile.echo.app'])
def echoWebContainerFile = file(ant.properties['dir.dist.lib'] + '/' + ant.properties['jarfile.echo.webcontainer'])

artifacts {
	archives file: echoAppFile, name: 'echo3-app', type: 'jar'
	archives file: echoWebContainerFile, name: 'echo3-webcontainer', type: 'jar'
}

uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			snapshotRepository(url:"dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/snapshot/") {
				authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
			}
			repository(url: "dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/release/") {
				authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
			}
			addFilter('echo3-app') {artifact, file ->
				artifact.name == 'echo3-app'
			}
			addFilter('echo3-webcontainer') {artifact, file ->
				artifact.name == 'echo3-webcontainer'
			}
			
			pom('echo3-app').version = ant.properties['release.version']
			pom('echo3-webcontainer').version = ant.properties['release.version']
		}
	}
}