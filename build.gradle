apply plugin: 'java'
apply plugin: 'maven'

group = 'org.maydesk'
sourceCompatibility = JavaVersion.VERSION_1_6
def cloudbeesAccountName = 'maydesk'
def cloudbeesUsername = 'maydesk-deploy'
def cloudbeesPassword = 'secret111'

configurations {
	deployerJars
	testCompile
}

repositories {
	mavenCentral()
}
    
sourceSets {
	main {
		java {
			srcDir 'src/server-java/app'
			srcDir 'src/server-java/webcontainer'
		}
	}
	
	test {
		java {
			srcDir 'src/server-java/app-test'
			srcDir 'src/server-java/webcontainer-test'
		}
	}
}

dependencies {
	compile "javax.servlet:servlet-api:2.5"
	testCompile "junit:junit:4.11"
	deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
	deployerJars 'org.apache.maven:maven-ant-tasks:2.1.0'
}

ant.importBuild 'gradleWrapper.xml'

// Ant build file needs the sevrlet.lib.jar to point to the correct location
ant.properties['servlet.lib.jar'] = configurations.compile.asPath
defaultTasks 'uploadArchives'

clean.dependsOn 'ant.clean'
uploadArchives.dependsOn 'ant.dist'

// app and webcontainer variables constitute duplicate names of the ant build file.
// The ant build file variables contain the version, and that's not suitable for our maven builds 
def app = 'echo3-app'
def echoAppFile = file(ant.properties['dir.dist.lib'] + '/' + ant.properties['jarfile.echo.app'])

def webcontainer = 'echo3-webcontainer'
def echoWebContainerFile = file(ant.properties['dir.dist.lib'] + '/' + ant.properties['jarfile.echo.webcontainer'])

artifacts {
	archives file: echoAppFile, name: app, type: 'jar'
	archives file: echoWebContainerFile, name: webcontainer, type: 'jar'
}

uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			snapshotRepository(url:"dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/snapshot/") {
				authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
			}
			repository(url: "dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/release/") {
				authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
			}
			addFilter(app) {artifact, file ->
				artifact.name == app
			}
			addFilter(webcontainer) {artifact, file ->
				artifact.name == webcontainer
			}
			
			pom(app).version = ant.properties['release.version']
			pom(webcontainer).version = ant.properties['release.version']
		}
	}
}